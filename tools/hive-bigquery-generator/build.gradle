plugins {
  id 'org.springframework.boot' version '2.7.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id "com.diffplug.spotless" version "5.9.0"
}

group = 'com.google.cloud.pso.bqexternalviewgenerator'
version = '1.0.2'
sourceCompatibility = '1.8'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.0'
  implementation 'org.projectlombok:lombok:1.18.24'

  implementation 'org.apache.hive:hive-common:3.1.3'
  implementation 'org.apache.hive:hive-metastore:3.1.3'
  implementation 'org.apache.hadoop:hadoop-common:3.3.2'
  implementation 'org.apache.hadoop:hadoop-mapreduce-client-core:3.3.2'

  // Google Services Deps
  implementation 'com.google.cloud:google-cloud-bigquery:2.13.2'

  annotationProcessor 'org.projectlombok:lombok:1.18.24'
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

  // Testing Dependencies
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation "junit:junit:4.13.2"
  testImplementation "com.google.truth:truth:1.1.3"
}

configurations {
  all {
    //exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'

  }
}

tasks.named('test') {
}

spotless {
  java {
    target "**/*.java"
    targetExclude "third_party/**"
    googleJavaFormat("1.7")
    removeUnusedImports()
    importOrder()
    trimTrailingWhitespace()
    endWithNewline()
    licenseHeader "/*\n" +
        " * Copyright \$YEAR Google\n" +
        " * This software is provided as-is, without warranty or representation for any use or purpose. \n" +
        " * Your use of it is subject to your agreement with Google.\n" +
        " */\n" +
        "\n"
  }

  freshmark {
    target '*.md'
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }

  format "gradle", {
    target "**/*.gradle"
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }

  format "misc", {
    target "**/.gitignore", "*.xml", "src/**/*.xml"
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
}
